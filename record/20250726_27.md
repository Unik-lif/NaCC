## 实验记录
继续解读工作，主要是搞清楚栈的使用

### 栈的初始化
栈的初始化位置主要是在_fw_end之后的部分，首先读取不同平台为各自的stack和heap设置的大小：
```C
	/* Preload HART details
	 * s7 -> HART Count
	 * s8 -> HART Stack Size
	 * s9 -> Heap Size
	 * s10 -> Heap Offset
	 */
	lla	a4, platform
#if __riscv_xlen > 32
	lwu	s7, SBI_PLATFORM_HART_COUNT_OFFSET(a4)
	lwu	s8, SBI_PLATFORM_HART_STACK_SIZE_OFFSET(a4)
	lwu	s9, SBI_PLATFORM_HEAP_SIZE_OFFSET(a4)
#else
	lw	s7, SBI_PLATFORM_HART_COUNT_OFFSET(a4)
	lw	s8, SBI_PLATFORM_HART_STACK_SIZE_OFFSET(a4)
	lw	s9, SBI_PLATFORM_HEAP_SIZE_OFFSET(a4)
#endif

	/* Setup scratch space for all the HARTs*/
	lla	tp, _fw_end
	// 如果只考虑栈的大小，总共要有多大的空间分出去，把所有的hart对应的栈空间s8算进去
	mul	a5, s7, s8
	// tp做个加法，到物理地址的高位，物理地址这边是从低地址增长到高地址的
	add	tp, tp, a5
	/* Setup heap base address */
	lla	s10, _fw_start
	// 记录了从栈空间底到_fw_start之间的offset值，也就是内存值，或者是heap开始的offset位置
	sub	s10, tp, s10
	// 再加上所需要的heap大小，于是这边就得到了内存总值
	add	tp, tp, s9
	/* Keep a copy of tp */
	// 将这边的内存总值，先存放到t3中
	add	t3, tp, zero
	/* Counter */
	li	t2, 1
	/* hartid 0 is mandated by ISA */
	li	t1, 0

	// 进入_scratch_init的其实是一个大循环，我们从hartid 0开始跑
_scratch_init:
	/*
	 * The following registers hold values that are computed before
	 * entering this block, and should remain unchanged.
	 *
	 * t3 -> the firmware end address
	 * s7 -> HART count
	 * s8 -> HART stack size
	 * s9 -> Heap Size
	 * s10 -> Heap Offset
	 */
	// 一开始，t3中存放着tp中包括heap和stack的总内存大小值
	add	tp, t3, zero
	// 先把heap区域减少掉
	sub	tp, tp, s9
	// 根据当前的hart id，确认a5
	mul	a5, s8, t1
	// boot hartid所对应的tp不需要修改，但其他的则会减掉s8 * t1
	sub	tp, tp, a5
	// 在heap减掉的位置基础上，再减去一个SCRATCH_SIZE
	li	a5, SBI_SCRATCH_SIZE
	// 从这个位置上，开始我们的tp，
	// 在virt场景下，对应的栈的大小恰好是两个页，于是闭环了
	sub	tp, tp, a5

	/* Initialize scratch space */
	/* Store fw_start and fw_size in scratch space */
	lla	a4, _fw_start
	// 记录了_fw_start的地址，并且将其和t3相减，由此知道fw_size这个值的大小，存放到a5，之后继续存放，总之就是存放了scratch目前已有的一些数据
	sub	a5, t3, a4
	REG_S	a4, SBI_SCRATCH_FW_START_OFFSET(tp)
	REG_S	a5, SBI_SCRATCH_FW_SIZE_OFFSET(tp)

	/* Store R/W section's offset in scratch space */
	lla	a5, _fw_rw_start
	sub	a5, a5, a4
	REG_S	a5, SBI_SCRATCH_FW_RW_OFFSET(tp)

	/* Store fw_heap_offset and fw_heap_size in scratch space */
	REG_S	s10, SBI_SCRATCH_FW_HEAP_OFFSET(tp)
	REG_S	s9, SBI_SCRATCH_FW_HEAP_SIZE_OFFSET(tp)

	/* Store next arg1 in scratch space */
	MOV_3R	s0, a0, s1, a1, s2, a2
	call	fw_next_arg1
	REG_S	a0, SBI_SCRATCH_NEXT_ARG1_OFFSET(tp)
	MOV_3R	a0, s0, a1, s1, a2, s2
	/* Store next address in scratch space */
	MOV_3R	s0, a0, s1, a1, s2, a2
	call	fw_next_addr
	REG_S	a0, SBI_SCRATCH_NEXT_ADDR_OFFSET(tp)
	MOV_3R	a0, s0, a1, s1, a2, s2
	/* Store next mode in scratch space */
	MOV_3R	s0, a0, s1, a1, s2, a2
	call	fw_next_mode
	REG_S	a0, SBI_SCRATCH_NEXT_MODE_OFFSET(tp)
	MOV_3R	a0, s0, a1, s1, a2, s2
	/* Store warm_boot address in scratch space */
	lla	a4, _start_warm
	REG_S	a4, SBI_SCRATCH_WARMBOOT_ADDR_OFFSET(tp)
	/* Store platform address in scratch space */
	lla	a4, platform
	REG_S	a4, SBI_SCRATCH_PLATFORM_ADDR_OFFSET(tp)
	/* Store hartid-to-scratch function address in scratch space */
	lla	a4, _hartid_to_scratch
	REG_S	a4, SBI_SCRATCH_HARTID_TO_SCRATCH_OFFSET(tp)
	/* Clear trap_context and tmp0 in scratch space */
	REG_S	zero, SBI_SCRATCH_TRAP_CONTEXT_OFFSET(tp)
	REG_S	zero, SBI_SCRATCH_TMP0_OFFSET(tp)
	/* Store firmware options in scratch space */
	MOV_3R	s0, a0, s1, a1, s2, a2
#ifdef FW_OPTIONS
	li	a0, FW_OPTIONS
#else
	call	fw_options
#endif
	REG_S	a0, SBI_SCRATCH_OPTIONS_OFFSET(tp)
	MOV_3R	a0, s0, a1, s1, a2, s2
	/* Store hart index in scratch space */
	REG_S	t1, SBI_SCRATCH_HARTINDEX_OFFSET(tp)
	/* Move to next scratch space */
	// 让t1指向下一个hart
	add	t1, t1, t2
	// 直到t1增长到和s7一样，也就是遍历了一遍hart之后，才离开初始化
	blt	t1, s7, _scratch_init
```
根据一些信息打印以及七月十七号做实验的一些记录，我们可以知道STACK_SIZE被设置成了2个页，但是其中地址低的一半被拿去放了scratch数据结构，于是每次根据hartid和stack_size计算得到位置后，还需要减去一个页的偏移量

在完成了这一部分初始化之后，我们进入_start_warm
```C
_start_warm:
	/* Reset all registers except ra, a0, a1, a2, a3 and a4 for non-boot HART */
	li	ra, 0
	// regs全部的清空，包括先前的tp等重要信息
	call	_reset_regs

	/* Disable all interrupts */
	csrw	CSR_MIE, zero

	/* Find HART count and HART stack size */
	// 这个在先前我们设置的stack区域以外，之前初始化的时候页短暂使用过，我们从中可以得知s8，也就是栈的大小
	// 之后想办法从_fw_end重新找到栈的位置就行了
	lla	a4, platform
#if __riscv_xlen > 32
	lwu	s7, SBI_PLATFORM_HART_COUNT_OFFSET(a4)
	lwu	s8, SBI_PLATFORM_HART_STACK_SIZE_OFFSET(a4)
#else
	lw	s7, SBI_PLATFORM_HART_COUNT_OFFSET(a4)
	lw	s8, SBI_PLATFORM_HART_STACK_SIZE_OFFSET(a4)
#endif
	REG_L	s9, SBI_PLATFORM_HART_INDEX2ID_OFFSET(a4)

	/* Find HART id */
	// 从寄存器中，确认hart id
	csrr	s6, CSR_MHARTID

	/* Find HART index */
	// 如果CSR_MHARTID中没有值，则说明没有成功正确地初始化，直接进入_start_hart
	beqz	s9, 3f
	li	a4, 0
1:
#if __riscv_xlen > 32
	lwu	a5, (s9)
#else
	lw	a5, (s9)
#endif
	// 如果a5和s6是一样的，则说明恒等映射，两个都是hart id，跳转到2，验证s6和s7的关系，当然s6会更小一些，否则异常，进入_start_hang
	beq	a5, s6, 2f
	add	s9, s9, 4
	add	a4, a4, 1
	blt	a4, s7, 1b
2:	add	s6, a4, zero
3:	bge	s6, s7, _start_hang

	/* Find the scratch space based on HART index */
	// 根据hart和stack_size，找到栈地址最高的位置，给tp加上这个值
	// 所有的hart在tp上得到的值是一样的
	lla	tp, _fw_end
	mul	a5, s7, s8
	add	tp, tp, a5
	// 但是s6对应的是hart id，这边计算得到需要减去的栈的个数，如果是hart id为0，则不需要减
	mul	a5, s8, s6
	sub	tp, tp, a5
	// stack之下的一个页，对应的就是当前这个hart id所对应的scratch数据结构所对应的位置
	li	a5, SBI_SCRATCH_SIZE
	// tp现在很自然地就指向了当前hart所对应的scratch地址
	sub	tp, tp, a5

	/* update the mscratch */
	// 于是很自然就把tp写到了mscratch寄存器中
	csrw	CSR_MSCRATCH, tp

	/* Setup stack */
	// stack也设置到了tp上，现在sp在其最高地址位置上，之后将会自然地压栈和降低其地址
	add	sp, tp, zero

	/* Setup trap handler */
	lla	a4, _trap_handler
	csrr	a5, CSR_MISA
	srli	a5, a5, ('H' - 'A')
	andi	a5, a5, 0x1
	beq	a5, zero, _skip_trap_handler_hyp
	lla	a4, _trap_handler_hyp
_skip_trap_handler_hyp:
	csrw	CSR_MTVEC, a4

	/* Clear MDT here again for all harts */
	CLEAR_MDT t0

	/* Initialize SBI runtime */
	csrr	a0, CSR_MSCRATCH
	call	sbi_init
```
到这里，我们完成了栈和stack的初始化，因此七月十七号的代码阅读笔记是有点问题的，更正如下
```
high addr
┌─────────────────┐
│ Hart 0 scratch  │
├─────────────────┤ <-- tp, sp. tp对应的是scratch，而sp对应的是stack，前者想high addr增长，而后者向lower addr增长
│ Hart 0 stack    │ 
├─────────────────┤
│ Hart 1 scratch  │
├─────────────────┤
│ Hart 1 stack    │  
├─────────────────┤
│       ...       │
├─────────────────┤
│ _fw_end         │ ← 固件结束地址
│ (firmware)      │
└─────────────────┘
low addr
```
### 中断上下文中栈和scratch的使用
#### 总体结构
由于先前没有特别分清上面的scratch和stack的具体地址排布情况，我对代码的理解出了一些偏差，但是现在读到这边，感觉好多了，也没那么模糊了，我们重新开始解读下面的部分
```C
// a4中存放了_trap_handler_hyp的入口地址
_skip_trap_handler_hyp:
	csrw	CSR_MTVEC, a4


	.section .entry, "ax", %progbits
	.align 3
	.globl _trap_handler_hyp
_trap_handler_hyp:
    // 这边是对于上下文的一些寄存工作
	TRAP_SAVE_AND_SETUP_SP_T0

#if __riscv_xlen == 32
	TRAP_SAVE_MEPC_MSTATUS 1
#else
	TRAP_SAVE_MEPC_MSTATUS 0
#endif

	TRAP_SAVE_GENERAL_REGS_EXCEPT_SP_T0

#if __riscv_xlen == 32
	TRAP_SAVE_INFO 1 1
#else
	TRAP_SAVE_INFO 0 1
#endif

	TRAP_CALL_C_ROUTINE

	TRAP_RESTORE_GENERAL_REGS_EXCEPT_A0_T0

#if __riscv_xlen == 32
	TRAP_RESTORE_MEPC_MSTATUS 1
#else
	TRAP_RESTORE_MEPC_MSTATUS 0
#endif

	TRAP_RESTORE_A0_T0

	mret
```
#### TRAP_SAVE_AND_SETUP_SP_T0函数的解读
```C
.macro	TRAP_SAVE_AND_SETUP_SP_T0
	/* Swap TP and MSCRATCH */
	csrrw	tp, CSR_MSCRATCH, tp

	/* Save T0 in scratch space */
	// 之后要用t0，于是先把现在的t0存一下
	REG_S	t0, SBI_SCRATCH_TMP0_OFFSET(tp)

	/*
	 * Set T0 to appropriate exception stack
	 *
	 * Came_From_M_Mode = ((MSTATUS.MPP < PRV_M) ? 1 : 0) - 1;
	 * Exception_Stack = TP ^ (Came_From_M_Mode & (SP ^ TP))
	 *
	 * Came_From_M_Mode = 0    ==>    Exception_Stack = TP
	 * Came_From_M_Mode = -1   ==>    Exception_Stack = SP
	 */
	// 检查MSTATUS的状态
	csrr	t0, CSR_MSTATUS
	// 右移11位，看MPP的值
	srl	t0, t0, MSTATUS_MPP_SHIFT
	// 做一个and
	and	t0, t0, PRV_M
	// 立即数比较，比较t0和PRV_M的值，若t0更小，表示异常来自于PRV_S或者PRV_U，t0返回1
	slti	t0, t0, PRV_M
	// 现在是0或者-1了
	add	t0, t0, -1
	// sp为sp和tp的异或
	xor	sp, sp, tp
	// 如果是m mode，那会得到sp^tp，反之为0
	and	t0, t0, sp
	// (sp^tp)^tp = sp，sp恢复了原来的值
	xor	sp, sp, tp
	// t0 = tp ^ t0，如果是m mode，则t0为sp，如果是其他mode，则t0为tp，实现二选一
	// 这挺自然的，如果是m mode同权限的trap，本质上只要操纵当前的栈就行了
	xor	t0, tp, t0

	/* Save original SP on exception stack */
	// 把sp存储到一个特别的位置上，是从最高的地址反向向前找的，下面的这个数是一个负的偏移量
	// SBI_TRAP_REGS_SIZE + SBI_TRAP_INFO_SIZE + __SIZEOF_POINTER__的这个值在对齐之后，就形成了SBI_TRAP_CONTEXT_SIZE
	// 以此我们可以推测出排布的方式，也是先放SBI_TRAP_REGS_SIZE，再放后头的
	// 等于先减一个偏移量，之后再加回来
	// 先存放一下现在的sp，因为我们马上就要使用sp寄存器了
	REG_S	sp, (SBI_TRAP_REGS_OFFSET(sp) - SBI_TRAP_CONTEXT_SIZE)(t0)

	/* Set SP to exception stack and make room for trap context */
	// 对sp从t0位置减一个SBI_TRAP_CONTEXT_SIZE的值，这个位置就是sp的起点
	add	sp, t0, -(SBI_TRAP_CONTEXT_SIZE)

	/* Restore T0 from scratch space */
	// t0寄存器已经使用过了，重新恢复过来
	REG_L	t0, SBI_SCRATCH_TMP0_OFFSET(tp)

	/* Save T0 on stack */
	// 把之前的t0，存放到sp对应的trap_regs数据结构区间中去了
	REG_S	t0, SBI_TRAP_REGS_OFFSET(t0)(sp)

	/* Swap TP and MSCRATCH */
	// 逆操作，可能tp在调度的时候会发生一些改变？不过也许这并不太重要，反正我们保证CSR_MSCRATCH中的值指向的恰好是当前hart对应的tp位置就行了
	// 也有可能因为我们在这个操作之后并不需要使用tp，在trap过程中只需要使用sp寄存器来做上下文处理就行了
	csrrw	tp, CSR_MSCRATCH, tp
.endm
```
#### TRAP_SAVE_MEPC_MSTATUS
根据riscv当前的位数来决定have_mstatush的走向
```C
.macro	TRAP_SAVE_MEPC_MSTATUS have_mstatush
	/* Save MEPC and MSTATUS CSRs */
	// 存放了MEPC和MSTATUS寄存器的值，32位的riscv架构有mstatush寄存器
	// 需要额外地再store一次
	csrr	t0, CSR_MEPC
	REG_S	t0, SBI_TRAP_REGS_OFFSET(mepc)(sp)
	csrr	t0, CSR_MSTATUS
	REG_S	t0, SBI_TRAP_REGS_OFFSET(mstatus)(sp)
	.if \have_mstatush
	csrr	t0, CSR_MSTATUSH
	REG_S	t0, SBI_TRAP_REGS_OFFSET(mstatusH)(sp)
	.else
	REG_S	zero, SBI_TRAP_REGS_OFFSET(mstatusH)(sp)
	.endif
.endm
```
#### TRAP_SAVE_GENERAL_REGS_EXCEPT_SP_T0
那这个就很简单了，反正我sp都有了，之后就全部存放一下就得了，和restore是逆操作，我们就不谈了
``` C
.macro	TRAP_SAVE_GENERAL_REGS_EXCEPT_SP_T0
	/* Save all general regisers except SP and T0 */
	REG_S	zero, SBI_TRAP_REGS_OFFSET(zero)(sp)
	REG_S	ra, SBI_TRAP_REGS_OFFSET(ra)(sp)
	REG_S	gp, SBI_TRAP_REGS_OFFSET(gp)(sp)
	REG_S	tp, SBI_TRAP_REGS_OFFSET(tp)(sp)
	REG_S	t1, SBI_TRAP_REGS_OFFSET(t1)(sp)
	REG_S	t2, SBI_TRAP_REGS_OFFSET(t2)(sp)
	REG_S	s0, SBI_TRAP_REGS_OFFSET(s0)(sp)
	REG_S	s1, SBI_TRAP_REGS_OFFSET(s1)(sp)
	REG_S	a0, SBI_TRAP_REGS_OFFSET(a0)(sp)
	REG_S	a1, SBI_TRAP_REGS_OFFSET(a1)(sp)
	REG_S	a2, SBI_TRAP_REGS_OFFSET(a2)(sp)
	REG_S	a3, SBI_TRAP_REGS_OFFSET(a3)(sp)
	REG_S	a4, SBI_TRAP_REGS_OFFSET(a4)(sp)
	REG_S	a5, SBI_TRAP_REGS_OFFSET(a5)(sp)
	REG_S	a6, SBI_TRAP_REGS_OFFSET(a6)(sp)
	REG_S	a7, SBI_TRAP_REGS_OFFSET(a7)(sp)
	REG_S	s2, SBI_TRAP_REGS_OFFSET(s2)(sp)
	REG_S	s3, SBI_TRAP_REGS_OFFSET(s3)(sp)
	REG_S	s4, SBI_TRAP_REGS_OFFSET(s4)(sp)
	REG_S	s5, SBI_TRAP_REGS_OFFSET(s5)(sp)
	REG_S	s6, SBI_TRAP_REGS_OFFSET(s6)(sp)
	REG_S	s7, SBI_TRAP_REGS_OFFSET(s7)(sp)
	REG_S	s8, SBI_TRAP_REGS_OFFSET(s8)(sp)
	REG_S	s9, SBI_TRAP_REGS_OFFSET(s9)(sp)
	REG_S	s10, SBI_TRAP_REGS_OFFSET(s10)(sp)
	REG_S	s11, SBI_TRAP_REGS_OFFSET(s11)(sp)
	REG_S	t3, SBI_TRAP_REGS_OFFSET(t3)(sp)
	REG_S	t4, SBI_TRAP_REGS_OFFSET(t4)(sp)
	REG_S	t5, SBI_TRAP_REGS_OFFSET(t5)(sp)
	REG_S	t6, SBI_TRAP_REGS_OFFSET(t6)(sp)
.endm
```
#### TRAP_SAVE_INFO
这边是除了普通寄存器以外，其他的trap信息存放使用的函数，它本质上是需要锁定到TRAP_INFO区域，然后对这些东西进行一个store，在我们的情况，两个参数分别是0和1
```C 
.macro	TRAP_SAVE_INFO have_mstatush have_h_extension
	csrr	t0, CSR_MCAUSE
	REG_S	t0, (SBI_TRAP_REGS_SIZE + SBI_TRAP_INFO_OFFSET(cause))(sp)
	csrr	t0, CSR_MTVAL
	REG_S	t0, (SBI_TRAP_REGS_SIZE + SBI_TRAP_INFO_OFFSET(tval))(sp)
.if \have_h_extension
	csrr	t0, CSR_MTVAL2
	REG_S	t0, (SBI_TRAP_REGS_SIZE + SBI_TRAP_INFO_OFFSET(tval2))(sp)
	csrr	t0, CSR_MTINST
	REG_S	t0, (SBI_TRAP_REGS_SIZE + SBI_TRAP_INFO_OFFSET(tinst))(sp)
	.if \have_mstatush
	csrr	t0, CSR_MSTATUSH
	srli	t0, t0, MSTATUSH_GVA_SHIFT
	.else
	csrr	t0, CSR_MSTATUS
	srli	t0, t0, MSTATUS_GVA_SHIFT
	.endif
	and	t0, t0, 0x1
.else
	REG_S	zero, (SBI_TRAP_REGS_SIZE + SBI_TRAP_INFO_OFFSET(tval2))(sp)
	REG_S	zero, (SBI_TRAP_REGS_SIZE + SBI_TRAP_INFO_OFFSET(tinst))(sp)
	li	t0, 0
.endif
	REG_S	t0, (SBI_TRAP_REGS_SIZE + SBI_TRAP_INFO_OFFSET(gva))(sp)

	/* We are ready to take another trap, clear MDT */
	CLEAR_MDT t0
.endm
```
看起来我们能复用一部分代码，但是前提是sp的位置一定要选好，不能随便乱选，否则就会出很大的问题
#### handler
```C
.macro	TRAP_CALL_C_ROUTINE
	/* Call C routine */
	add	a0, sp, zero
	call	sbi_trap_handler
.endm
```
这好像就没什么好说的了

### 思考
到这里我们完成了这个分析，可以看出代码不是那么好读，花了一些时间。

于是回到我们现在的目的，显然我们可以复用不少这边的代码，但是应该如何恰如其分地使用呢？

显然我们需要开辟一个区域来存放上下文信息，这个区域我们可以放在scratch区域，需要在secure monitor中特地通过allocate来进行使用

对于这个区域，我们需要让每个核，对这个区域都要做一个初始化，之后就和trap发生的使用逻辑一样，我们用同样的方式，尽可能复用这边本身的trap_handler的代码